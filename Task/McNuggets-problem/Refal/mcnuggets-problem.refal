$ENTRY Go {
    , <Iota 0 6 100>: e.A
    , <Iota 0 9 100>: e.B
    , <Iota 0 20 100>: e.C
    , <Iota 0 1 100>: e.Nums
    , <SumPairs (e.A) (<SumPairs (e.B) (e.C)>)>: e.Nuggets
    , <RemoveAll (e.Nuggets) e.Nums>: e.NonNuggets
    , e.NonNuggets: e.X s.Last
    = <Prout 'The largest non-McNuggets number < 100 is: ' s.Last>;
};

SumPairs {
    () (e.Y) = ;
    (s.I e.X) (e.Y) = <SumPairs1 s.I (e.Y)> <SumPairs (e.X) (e.Y)>;
};

SumPairs1 {
    s.I () = ;
    s.I (s.X e.X) = <+ s.I s.X> <SumPairs1 s.I (e.X)>;
};

Remove {
    s.I e.X s.I e.Y = e.X <Remove s.I e.Y>;
    s.I e.X = e.X;
};

RemoveAll {
    () e.X = e.X;
    (s.R e.R) e.X = <RemoveAll (e.R) <Remove s.R e.X>>;
};

Iota {
    s.Start s.Step s.End, <Compare s.Start s.End>: {
        '+' = ;
        s.X = s.Start <Iota <+ s.Start s.Step> s.Step s.End>;
    };
};
