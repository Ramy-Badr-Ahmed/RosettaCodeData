 100  READ UX,UY
 110  DIM SCALARS(UX,UY)
 120  FOR N = 1 TO UX
 130  FOR M = 1 TO UY
 140  READ SCALARS(N,M)
 150  NEXT M,N
 160  DATA10,4
 170  DATA1,2,3,0,0,1,2,3
 180  DATA1,0,3,4,1,2,0,0
 190  DATA0,0,0,0,0,0,0,0
 200  DATA1,1,1,0,-1,-1,-1,0
 210  DATA-1,-2,0,-3,-1,0,0,0
 220  FOR N = 1 TO UX
 230      CADENA$ = ""
 240      FOR M = 1 TO UY
 250          SCALAR = SCALARS(N,M)
 260          IF SCALAR THEN CADENA$ = CADENA$ +  CHR$ (44 -  SGN (SCALAR)) +  MID$ ( STR$ ( ABS (SCALAR)) + "*",1,255 * ( ABS (SCALAR) <  > 1)) + "E" +  STR$ (M)
 270      NEXT M
 280      IF CADENA$ = "" THEN CADENA$ = "0"
 290      IF  LEFT$ (CADENA$,1) = "+" THEN CADENA$ =  RIGHT$ (CADENA$, LEN (CADENA$) - 1)
 300      PRINT CADENA$
 310  NEXT N
