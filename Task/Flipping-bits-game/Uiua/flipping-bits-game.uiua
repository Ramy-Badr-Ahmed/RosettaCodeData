S ← 9
Fr ← ⊙◌⍜⊡¬⊙.
Fc ← ⊙◌⍉Fr⊙(⍉.)
Shuffle ← ⍥(⟨Fr|Fc⟩<0.5⚂ ⌊×S⚂)×3S
T ← Shuffle ↯S_S 0 # Target setup. Omit `Shuffle` for plain grid.
D ← /+/+⌵-T

# Could use A*, but this is easily solved without it.
# ◌astar(⊂⊃(≡Fr|≡Fc)⇡S¤|÷S D|=0D)
⟜[∘] Shuffle T # Shuffle and keep a copy for the output.
◌⍢(

  ⊂⊃(≡Fr|≡Fc)⇡S¤ # All neighbours.
  ⊚=/↧.≡D.       # Find min dist, get indices that give that.
  ⊡⊢             # Pick first one. ⊡(⊡⌊×⚂⧻.) to pick at random.
  ⊙⊂.            # Save a copy of the new state.
| ≠0D
)
⟜($"_ steps."-1⧻)⇌
