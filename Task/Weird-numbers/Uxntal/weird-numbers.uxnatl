( uxncli weird.rom )

|10 @Console &vector $2 &read $1 &pad $5 &write $1 &error $1
%MOD2 { DIV2k MUL2 SUB2 }
|0000
@c $2
@n $2
@i $1

|0100
( init )
;str ptext #0a .Console/write DEO

( main )
#000c
&sieve
	#0000 .c STZ2
	DUP2 DUP2 .n STZ2 divisors
	abundance ?{ semiperfection }
	clear
	INC2 .i LDZ #19 LTH ?&sieve
	POP2
BRK

@pdec ( short* -- )
	#2710 [ LIT2r 00fb ]
	&while ( -- )
		DIV2k #000a DIV2k MUL2 SUB2 SWPr EQUk OVR STHkr EQU AND ?{
			DUP [ LIT "0 ] ADD #19 DEO
			INCr }
		POP2 #000a DIV2 SWPr INCr STHkr ?&while
	POP2r POP2 POP2 JMP2r

@ptext ( str* -- )
	&while
		LDAk .Console/write DEO
		INC2 LDAk ?&while
	POP2
	JMP2r

@divisors ( short* -- )
	#0000
		&loop
		OVR2 OVR2 MOD2 #0000 GTH2 ?{ DUP2 ;divs .c LDZ2 STH2k ADD2 STA2 STH2r INC2 INC2 .c STZ2 }
		INC2 GTH2k ?&loop POP2 POP2
	JMP2r

@abundance ( -- flag )
	#0000 STH2
	;divs
	&while
		LDA2k STH2 ADD2r
		#0002 ADD2
		LDA2k NIP ?&while
		POP2
	STH2r .n LDZ2 GTH2 ?{ #01 JMP2r } #00 ( returns 1 when not abundant to skip semiperfection check in main )
	JMP2r

@semiperfection ( -- )
	#0001 ;divs .c LDZ2 ADD2 STA2
	#0000
	&for
		.n LDZ2
		&loop
			OVR2 OVR2 SWP2 #0002 MUL2 ;divs ADD2 LDA2  SUB2 #0002 MUL2
			;divs .c LDZ2 ADD2 ADD2 LDA2
			#0000 EQU2 ?{ #0001 OVR2 #0002 MUL2 ;divs .c LDZ2 ADD2 ADD2 STA2 }
			#0001 SUB2
			OVR2 #0002 MUL2 ;divs ADD2 LDA2 GTH2k STH EQU2k STHr ORA NIP NIP ?&loop POP2
			
		INC2 DUP2 .c LDZ2 #0002 DIV2 LTH2 ?&for POP2
	;divs .c LDZ2 ADD2 .n LDZ2 #0002 MUL2 ADD2 LDA2 NIP ?{ .n LDZ2 pdec #20 #18 DEO .i LDZk INC SWP STZ JMP2r }
	JMP2r

@clear ( -- )
	;divs
	.n LDZ2 .c LDZ2 ADD2
	&while
		SWP2 #0000 SWP2 STA2k NIP2
		INC2 INC2
		SWP2 #0001 SUB2 #0000 GTH2k NIP NIP ?&while POP2 POP2
	JMP2r

@str "The 20 "first 20 "25 20 "weird 20 "numbers: 00
@divs
