program levenshtein_distance;
    tests := {['kitten', 'sitting'], ['rosettacode', 'raisethysword']};
    loop for dest = tests(src) do
        print(src + ' -> ' + dest + ': ' + str levenshtein(src, dest));
    end loop;

    proc levenshtein(s, t);
        d := {};
        loop for i in [1..#s] do
            d(i,0) := i;
        end loop;
        loop for j in [1..#t] do
            d(0,j) := j;
        end loop;
        loop for j in [1..#t] do
            loop for i in [1..#s] do
                d(i,j) := min/[
                    (d(i-1,j) ? 0) + 1,
                    (d(i,j-1) ? 0) + 1,
                    (d(i-1,j-1) ? 0) + if s(i) = t(j) then 0 else 1 end
                ];
            end loop;
        end loop;
        return d(#s, #t);
    end proc;
end program;
